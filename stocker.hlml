# Stocks an inventory with phase-fabric and silicon using a mono.

using mlog::itemCapacity;
using mlog::range;
using mlog::totalItems;
using mlog::x;
using mlog::y;

## Inventory that is stocked.
link container1 as stocked;

## Number of items where below restocking will commence.
const trigger_amount = 20;

entrypoint {
  stock_item(stocked, mlog::mega, mlog::silicon);
  stock_item(stocked, mlog::mega, mlog::phase_fabric);
}

## Stocks the given inventory with a carrier of the given unit type with items
## of the given item type.
proc stock_item(inventory, carrier_type, item_type) {
  # Check if there is a need to restock the inventory.
  var inventory_amount = inventory.item_type;
  if inventory_amount >= trigger_amount { return; }

  # Bind the unit.
  mlog::ubind(carrier_type);

  # Locate the core.
  var core_x;
  var core_y;
  var core;
  mlog::ulocate_building_core(mlog::false, core_x, core_y, mlog::null, core);

  # Pick up the items from the core.
  lib::approach_to(core_x, core_y);
  lib::drop_all_to(core);
  lib::take_from(core, item_type);

  # Drop the items to the inventory.
  lib::approach_to(inventory.x, inventory.y);
  lib::drop_all_to(inventory);

  # Release the unit.
  mlog::ucontrol_unbind();
}
